;+--------------------------------------------------------------------------
; Эта TSR программа выводит окно средствами BIOS по нажатию F12
; выгрузка:
; >имяпрог /off (например: lab2 /off)
;+--------------------------------------------------------------------------
code_seg segment
        ASSUME  CS:CODE_SEG,DS:code_seg,ES:code_seg
	org 100h
start:
    jmp begin
;----------------------------------------------------------------------------
old_2Fh  	DD  ?
old_09h     DD  ?
old_1Ch		DD	?
;----------------------------------------------------------------------------
flag        DB  0
high_Y      DB  17	; координаты окна
left_X      DB  50	; координаты окна
low_Y       DB  27	; координаты окна
right_X     DB  69	; координаты окна
;
page_num    DB  0
coord_Y     DB  20	; Y координата сообщения в окне
coord_X     DB  55	; X координата сообщения в окне
size_		DW	10
BUFFER			DB	'ABCDEFGHIJKLM'	; сообщение выводимое в окно
;============================================================================
new_1Ch	proc	far
	pushf
	cmp		CS:flag,	0
	je		no_flag
	mov		CS:flag,	0
;-------------------- Вывод окна средствами BIOS ---------------------------
;
            push    BX	; сохранение используемых регистров в стеке
            push    CX	; сохранение используемых регистров в стеке
            push    DX	; сохранение используемых регистров в стеке
			push	DS	; сохранение используемых регистров в стеке
			;
			push	CS	;	настройка DS
			pop		DS	;				на наш сегмент, т.е DS=CS
;----------------------------------------------------------------------------
        mov     AX, 0600h      ; Задание окна
        mov     BH, 70h        ; Атрибут черный по серому
        mov     CH, high_Y     ; Ко-
        mov     CL, left_X     ;    ор-
        mov     DH, low_Y      ;       ди-
        mov     DL, right_X    ;          наты окна
        int 10h
;----------------------------------------------------------------------------
; ------------------------ Позиционируем курсор -----------------------------
        mov     AH,02h          ; Функция позиционирования
        mov     BH,page_num  ; Видеостраница
        mov     DH,coord_Y   ; Строка
        mov     DL,coord_X   ; Столбец
        int 10h
		mov     CX,	size_
		mov     BX, offset CS:BUFFER
		mov     AH,0Eh              ; Функция вывода одного символа
next_sym:
        mov     AL,CS:[BX]          ; Символ в AL
        inc     BX                  ; Сдвиг по строке
        int     10h                 ;
        loop    next_sym            ; Цикл по строке
;

			pop		DS	; восстановление регистров из стека в порядке LIFO
            pop     DX
            pop     CX
            pop     BX
no_flag:
	popf
	jmp		dword ptr CS:[old_1Ch]
;---------------------------------------------------------------------------	
new_1Ch	endp
;============================================================================
new_09h proc far
;
    pushf
	push    AX
    in      AL,60h      				; Введем scan-code
    cmp     AL,58h      				; Это скен-код <F12>
    je      hotkey      				; Yes
    pop     AX          				; No. Восстановим AX
	popf
    jmp     dword ptr CS:[old_09h]  	; В системный обработчик без возврата
hotkey:
	inc		CS:flag						; it's hotkey
    pop     AX
	popf
    jmp		dword ptr CS:[old_09h]
new_09h     endp
;============================================================================
new_2Fh proc far
    cmp     AH,0C8h         ; Наш номер?
    jne     Pass_2Fh        ; Нет, на выход
    cmp     AL,00h          ; Подфункция проверки на повторную установку?
    je      inst            ; Программа уже установлена
    cmp     AL,01h          ; Подфункция выгрузки?
    je      unins           ; Да, на выгрузку
    jmp     short Pass_2Fh  ; Неизвестная подфункция - на выход
inst:
    mov     AL,0FFh         ; Сообщим о невозможности повторной установки
    iret
Pass_2Fh:
    jmp dword PTR CS:[old_2Fh]
;
; -------------- Проверка - возможна ли выгрузка программы из памяти ? ------
unins:
    push    BX
    push    CX
    push    DX
    push    ES
;
    mov     CX,CS   ; Пригодится для сравнения, т.к. с CS сравнивать нельзя
;=========================================================================
    mov     AX,3509h    ; Проверить вектор 09h
    int     21h ; Функция 35h в AL - номер прерывания. Возврат-вектор в ES:BX
;
    mov     DX,ES
    cmp     CX,DX
    jne     Not_remove
;
    cmp     BX, offset CS:new_09h
    jne     Not_remove
;===========================================================================
   mov     AX,351Ch    ; Проверить вектор 1Ch
    int     21h ; Функция 35h в AL - номер прерывания. Возврат-вектор в ES:BX
;
    mov     DX,ES
    cmp     CX,DX
    jne     Not_remove
;
    cmp     BX, offset CS:new_1Ch
    jne     Not_remove
;===========================================================================
    mov     AX,352Fh    ; Проверить вектор 2Fh
    int     21h ; Функция 35h в AL - номер прерывания. Возврат-вектор в ES:BX
;
    mov     DX,ES
    cmp     CX,DX
    jne     Not_remove
;
    cmp     BX, offset CS:new_2Fh
    jne     Not_remove
; ---------------------- Выгрузка программы из памяти ---------------------
;
    push    DS
;==========================================================================
    lds     DX, CS:old_1Ch   	; 
    mov     AX,251Ch        	; Заполнение вектора старым содержимым
    int     21h
;==========================================================================
   lds     DX, CS:old_09h   	;
    mov     AX,2509h        	; Заполнение вектора старым содержимым
    int     21h
;==========================================================================
    lds     DX, CS:old_2Fh   	; Эта команда эквивалентна следующим двум
;    mov     DX, word ptr old_2Fh
;    mov     DS, word ptr old_2Fh+2
    mov     AX,252Fh
    int     21h
;===========================================================================
    pop     DS
;
    mov     ES,CS:2Ch       ; ES -> окружение
    mov     AH, 49h         ; Функция освобождения блока памяти
    int     21h
;
    mov     AX, CS
    mov     ES, AX          ; ES -> PSP выгрузим саму программу
    mov     AH, 49h         ; Функция освобождения блока памяти
    int     21h
;
    mov     AL,0Fh          ; Признак успешной выгрузки
    jmp     short pop_ret
Not_remove:
    mov     AL,0F0h          ; Признак - выгружать нельзя
pop_ret:
    pop     ES
    pop     DX
    pop     CX
    pop     BX
;
    iret
new_2Fh endp
;============================================================================
begin:
        mov CL,ES:80h       ; Длина хвоста в PSP
        cmp CL,0            ; Длина хвоста=0?
        je  check_install   ; Да, программа запущена без параметров,
                            ; попробуем установить
        xor CH,CH       ; CX=CL= длина хвоста
        cld             ; DF=0 - флаг направления вперед
        mov DI, 81h     ; ES:DI-> начало хвоста в PSP
        mov SI,offset key   ; DS:SI-> поле key
        mov AL,' '          ; Уберем пробелы из начала хвоста
repe    scasb   ; Сканируем хвост пока пробелы
                ; AL - (ES:DI) -> флаги процессора
                ; повторять пока элементы равны
        dec DI          ; DI-> на первый символ после пробелов
        mov CX, 4       ; ожидаемая длина команды
repe    cmpsb   ; Сравниваем введенный хвост с ожидаемым
                ; (DS:DI)-(ES:DI) -> флаги процессора
        jne check_install ; Неизвестная команда - попробуем установить
        inc flag_off
; Проверим, не установлена ли уже эта программа
check_install:
        mov AX,0C800h   ; AH=0C8h номер процесса C8h
                        ; AL=00h -дать статус установки процесса
        int 2Fh         ; мультиплексное прерывание
        cmp AL,0FFh
        je  already_ins ; возвращает AL=0FFh если установлена
;----------------------------------------------------------------------------
    cmp flag_off,1
    je  xm_stranno
;----------------------------------------------------------------------------
    mov AX,352Fh                      	;   получить
										;   вектор
    int 21h                           	;   прерывания  2Fh
    mov word ptr old_2Fh,BX    		;   ES:BX - вектор
    mov word ptr old_2Fh+2,ES  		;
;
    mov DX,offset new_2Fh           	;   получить смещение точки входа в новый
										;   обработчик на DX
    mov AX,252Fh                    	;   функция установки прерывания
										;   изменить вектор 2Fh
    int 21h  							; AL - номер прерыв. DS:DX - указатель программы обработки прер.
;============================================================================
    mov AX,3509h                       	;   получить
										;   вектор
    int 21h                            	;   прерывания  09h
    mov word ptr old_09h,BX    		;   ES:BX - вектор
    mov word ptr old_09h+2,ES  		;
    mov DX,offset new_09h           	;   получить смещение точки входа в новый
;                                   	;   обработчик на DX
    mov AX,2509h                       	;   функция установки прерывания
                                        ;   изменить вектор 09h
    int 21h 							;   AL - номер прерыв. DS:DX - указатель программы обработки прер.
;============================================================================
   mov AX,351Ch                        	;   получить
                                        ;   вектор
    int 21h                            	;   прерывания  1Ch
    mov word ptr old_1Ch,BX    		;   ES:BX - вектор
    mov word ptr old_1Ch+2,ES  		;
    mov DX,offset new_1Ch           	;   получить смещение точки входа в новый
;                                   	;   обработчик на DX
    mov AX,251Ch                       	;   функция установки прерывания
										;   изменить вектор 1Ch
    int 21h 							;   AL - номер прерыв. DS:DX - указатель программы обработки прер.
        mov DX,offset msg1  			; Сообщение об установке
        call    print
;----------------------------------------------------------------------------
    mov DX,offset   begin           	;   оставить программу ...
    int 27h                         	;   ... резидентной и выйти
;============================================================================
already_ins:
        cmp flag_off,1      			; Запрос на выгрузку установлен?
        je  uninstall       			; Да, на выгрузку
        lea DX,msg          			; Вывод на экран сообщения: already installed!
        call    print
        int 20h
; ------------------ Выгрузка -----------------------------------------------
 uninstall:
        mov AX,0C801h  					; AH=0C8h номер процесса C8h, подфункция 01h-выгрузка
        int 2Fh             			; мультиплексное прерывание
        cmp AL,0F0h
        je  not_sucsess
        cmp AL,0Fh
        jne not_sucsess
        mov DX,offset msg2  			; Сообщение о выгрузке
        call    print
        int 20h
not_sucsess:
        mov DX,offset msg3  			; Сообщение, что выгрузка невозможна
        call    print
        int 20h
xm_stranno:
        mov DX,offset msg4  			; Сообщение, программы нет, а пользователь
        call    print       			; дает команду выгрузки
        int 20h
;----------------------------------------------------------------------------
key         DB  '/off'
flag_off    DB  0
msg         DB  'already '
msg1        DB  'installed',13,10,'$'
msg4        DB  'just '
msg3        DB  'not '
msg2        DB  'uninstalled',13,10,'$'
;============================================================================
PRINT       PROC NEAR
    MOV AH,09H
    INT 21H
    RET
PRINT       ENDP
;;============================================================================
code_seg ends
         end start
